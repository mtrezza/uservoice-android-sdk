buildscript {
    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

android {
    compileSdkVersion 28

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 17
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}

afterEvaluate {
    publishing {
        publications {
            create('mavenJava', MavenPublication) {
                groupId = 'com.github.mtrezza'
                artifactId = 'uservoice-android-sdk'
                version = '1.3.0'

                // Add the AAR file produced by the Android build
                artifact("$buildDir/outputs/aar/${project.name}-release.aar")

                // Add the sources JAR
                artifact(sourcesJar) {
                    classifier = 'sources'
                }

                // Additional POM details can be added here if needed (licenses, developers, etc.)
            }
        }

        repositories {
            maven {
                url = uri("file:///Users/austin/tmp/")
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.android.support:support-core-utils:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'oauth.signpost:signpost-core:2.1.1'
}
